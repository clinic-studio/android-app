// Plugins
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

// Configuration
android {
    // Default configuration
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26

        applicationId 'es.clinicstudio.app'
        versionCode 3
        versionName '1.0.2'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Build configuration
        buildConfigField 'String', 'BASE_URL', '"http://clinicstudio.es/wp-json/wp/v2/"'
        buildConfigField 'String', 'TOTAL_PAGES_HEADER', '"X-WP-TotalPages"'
        buildConfigField 'String', 'TOTAL_RECORDS_HEADER', '"X-WP-Total"'
    }

    // APK signing configuration
    signingConfigs {
        release {
            storeFile file('../buildsystem/keystore/upload-keystore.jks')
            storePassword System.getenv('CLINIC_STUDIO_KSTOREPWD')

            keyAlias 'clinicstudio.es'
            keyPassword System.getenv('CLINIC_STUDIO_KEYPWD')
        }
    }

    // Build types configuration
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".dev"
            debuggable true
        }
    }

    // Flavors configuration
    flavorDimensions 'mode'

    productFlavors {
        production {
            dimension 'mode'

            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all {
        variant ->
            if ('release' == variant.getBuildType().getName()) {
                variant.outputs.all { outputFileName = "${variant.applicationId}-build_${defaultConfig.versionCode}-${defaultConfig.versionName}-${variant.flavorName}-${variant.buildType.name}.apk" }

                variant.assemble.doLast {
                    copy {
                        from variant.mappingFile
                        into "${rootDir}/buildsystem/mapping"
                        rename { String fileName ->
                            "mapping-${variant.applicationId}-build_${defaultConfig.versionCode}-${defaultConfig.versionName}-${variant.flavorName}-${variant.buildType.name}.txt"
                        }
                    }
                }
            }
    }

    // Annotation processors configuration
    kapt {
        generateStubs = true
    }
}

// Module dependencies
dependencies {
    // Annotation processors
    kapt 'com.google.dagger:dagger-compiler:2.11'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'
    kapt 'com.github.bumptech.glide:compiler:4.2.0'


    // App dependencies
    // Android support libraries
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'

    // Kotlin dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Dagger
    implementation 'com.google.dagger:dagger:2.11'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    // Joda Time
    compile 'joda-time:joda-time:2.9.9'

    // ReactiveX
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.4'

    // Butterknife
    implementation 'com.jakewharton:butterknife:8.8.1'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.2.0'

    // Blurry
    implementation 'jp.wasabeef:blurry:2.1.1'

    // Glimmer module
    implementation project(':glimmer')

    // Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.7.0@aar') { transitive = true }


    // Android test dependencies
    // Espresso
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Unit test dependencies
    testImplementation 'junit:junit:4.12'
}

// More plugins (these ones NEED to be at the bottom)
apply plugin: 'com.google.gms.google-services'